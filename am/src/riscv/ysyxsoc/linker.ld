/*
 * 地址空间 (Address Space)
 * -------------------------------------------------------------------------
 * [FLASH] (0x30000000 - 0x30FFFFFF)  <-- 程序存储/启动区 (XIP)
 * |-----------------------|
 * |       *(entry)        |  <-- _start (bootloader, VMA=LMA=Flash, XIP 执行)
 * |-----------------------|
 * |   .text LMA           |  <-- 代码加载地址 (由 _sram_lma 指向)
 * |-----------------------|
 * |   .rodata LMA         |  <-- 只读常量加载地址
 * |-----------------------|
 * |   .data LMA           |  <-- 初始化数据加载地址
 * |-----------------------|
 * -------------------------------------------------------------------------
 *
 * -------------------------------------------------------------------------
 * [SRAM] (0x0f000000 - 0x0f001FFF)  <-- 代码+数据运行区
 * |-----------------------| <--- 0x0f000000 (_sram_start)
 * |   .text VMA           |  <-- 代码运行地址 (由 bootloader 从 Flash 搬运)
 * |-----------------------|
 * |   .rodata VMA         |  <-- 只读常量运行地址
 * |-----------------------|
 * |   .data/.sdata VMA    |  <-- 全局初始化变量
 * |-----------------------| <--- _data_end
 * |       .bss            |  <-- 全局清零变量
 * |-----------------------| <--- _bss_end
 * |       (空闲)          |
 * |          |            |
 * |          V            |
 * |       Stack (栈)      |  <-- 向下增长 (函数调用)
 * |-----------------------| <--- _stack_pointer (0x0f002000)
 * -------------------------------------------------------------------------
 *
 * -------------------------------------------------------------------------
 * [PSRAM] (0x80000000 - 0x803FFFFF)  <-- 外部 PSRAM（4MB）
 * |-----------------------| <--- _heap_start (0x80000000)
 * |       Heap (堆)       |
 * |          |            |  <-- 向上增长 (malloc)
 * |          V            |
 * |-----------------------| <--- _heap_end (0x80400000)
 * -------------------------------------------------------------------------
 */

ENTRY(_start)

MEMORY {
  FLASH (rx)  : ORIGIN = 0x30000000, LENGTH = 16M
  SRAM  (rwx) : ORIGIN = 0x0f000000, LENGTH = 8K
}

SECTIONS {
  .entry : {
    *(entry)
  } > FLASH

  .text : {
    _sram_start = .;
    *(.text*)
  } > SRAM AT > FLASH

  .rodata : {
    *(.rodata*)
    . = ALIGN(4);
  } > SRAM AT > FLASH

  .data : {
    _data_start = .;
    *(.data*)
    *(.sdata*)
    _data_end = .;
  } > SRAM AT > FLASH

  _sram_lma = LOADADDR(.text);

  .bss : {
    _bss_start = .;
    *(.bss*)
    *(.sbss*)
    *(.scommon)
    _bss_end = .;
  } > SRAM

  _heap_start = 0x80000000;
  _heap_end   = 0x80400000;
  _stack_pointer = ORIGIN(SRAM) + LENGTH(SRAM);
}
