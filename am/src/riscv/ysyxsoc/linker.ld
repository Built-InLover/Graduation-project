/*
 * 地址空间 (Address Space)
 * -------------------------------------------------------------------------
 * [FLASH] (0x30000000 - 0x30FFFFFF)  <-- 程序存储/启动区 (XIP)
 * |-----------------------|
 * |       *(entry)        |  <-- _start (复位向量入口)
 * |-----------------------|
 * |       *(.text*)       |  <-- 所有的 C 代码 (只读)
 * |-----------------------|
 * |      *(.rodata*)      |  <-- 只读常量 (字符串等)
 * |-----------------------|
 * |   .data 的初始值       |  <-- 加载地址 (LMA), 由 _data_lma 指向
 * |   (LMA Copy)          |      (烧录时存放在此)
 * |-----------------------|
 * -------------------------------------------------------------------------
 *
 * -------------------------------------------------------------------------
 * [SRAM] (0x0f000000 - 0x0f001FFF)  <-- 数据运行区
 * |-----------------------| <--- 0x0f000000 (ORIGIN)
 * |   .data/.sdata (VMA)  |  <-- 运行地址 (VMA), 由 _data_start 指向
 * |   (全局初始化变量)     |      (由启动代码从 FLASH 搬运过来)
 * |-----------------------| <--- _data_end
 * |       .bss            |  <-- 由 _bss_start 指向
 * |   (全局清零变量)       |      (由启动代码手动清零)
 * |-----------------------| <--- _bss_end
 * |       Stack (栈)      |
 * |          |            |  <-- 向下增长 (函数调用)
 * |          V            |
 * |-----------------------| <--- _stack_pointer (0x0f002000)
 * -------------------------------------------------------------------------
 *
 * -------------------------------------------------------------------------
 * [PSRAM] (0x80000000 - 0x803FFFFF)  <-- 外部 PSRAM（4MB）
 * |-----------------------| <--- _heap_start (0x80000000)
 * |       Heap (堆)       |
 * |          |            |  <-- 向上增长 (malloc)
 * |          V            |
 * |-----------------------| <--- _heap_end (0x80400000)
 * -------------------------------------------------------------------------
 */

ENTRY(_start)

MEMORY {
  FLASH (rx)  : ORIGIN = 0x30000000, LENGTH = 16M
  SRAM  (rwx) : ORIGIN = 0x0f000000, LENGTH = 8K
}

SECTIONS {
  .text : {
    *(entry)
    *(.text*)
  } > FLASH

  .rodata : {
    *(.rodata*)
    . = ALIGN(4);
  } > FLASH

  .data : {
    _data_start = .;
    *(.data*)
    *(.sdata*)
    _data_end = .;
  } > SRAM AT > FLASH

  _data_lma = LOADADDR(.data);

  .bss : {
    _bss_start = .;
    *(.bss*)
    *(.sbss*)
    *(.scommon)
    _bss_end = .;
  } > SRAM

  _heap_start = 0x80000000;
  _heap_end   = 0x80001000;
  _stack_top = ORIGIN(SRAM) + LENGTH(SRAM) - 0x1000;
  _stack_pointer = ORIGIN(SRAM) + LENGTH(SRAM);
}
